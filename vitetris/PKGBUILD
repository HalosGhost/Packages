# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=vitetris
pkgver=0.57
pkgrel=1
pkgdesc='A terminal-based Tetris clone with 2-player mode'
arch=('i686' 'x86_64')
url='http://victornils.net/tetris'
license=('BSD')
depends=('ncurses')
makedepends=('patch')
options=(!makeflags)
source=("http://victornils.net/tetris/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-makefile.diff")
md5sums=('07d02ee03e2edd66a8741729e237f21f'
         '5510adbb8ff3153466aa58c90d5765d8')

prepare() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   patch -Np0 -i "${srcdir}/${pkgname}-makefile.diff"
}

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   ./configure \
      --prefix="${pkgdir}/usr" \
      --docdir="${pkgdir}/usr/share/${pkgname}" \
      --without-x \
      --with-ncurses \
      --without-joystick

   make
   make gameserver
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   make install
   install -Dm755 gameserver "${pkgdir}/usr/bin/${pkgname}-gameserver"

   install -d -m775 "${pkgdir}/var/games/"
   install -Dm644 -g games /dev/null "${pkgdir}/var/games/${pkgname}/hiscores"

   install -Dm644 licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
