# Maintainer: Sam Stuewe <halosghost at archlinux dot info>
# Contributor: VÃ­tor Ferreira <vitor.dominor@gmail.com>

_name=xboxdrv
_branch=stable
pkgname="${_name}-stable-git"
pkgver=1110.ga52b07c
pkgrel=1
pkgdesc="A configurable userspace Xbox (360) gamepad driver"
arch=('i686' 'x86_64')
url="https://github.com/Grumbel/xboxdrv"
license=('GPL3')
depends=('libx11' 'dbus-glib' 'libusbx')
makedepends=('git' 'scons' 'boost' 'pkg-config' 'libx11' 'dbus-glib' 'libusb')
provides=('xboxdrv')
conflicts=('xboxdrv')
source=("${_name}::${url//https/git}.git#branch=${_branch}"
        "${_name}.conf"
        "${_name}.service")
sha256sums=('SKIP'
            'f155dd059faecafa60ecaa0988aec815ee0c58f1af45075de82ae10c31db2750'
            '51387a52a97d2e004a4160432131f18326e9ae655447694e170f18bdb8d7204f')

pkgver() {
  cd "${_name}"
  echo "$(git rev-list --count HEAD).$(git describe --always|cut -d '-' -f3)"
}

build() {
  cd "${_name}"
  make
}

package() {
  install -Dm755 "${_name}.service" "$pkgdir/usr/lib/systemd/system/${_name}.service"
  install -Dm644 "${_name}.conf" "$pkgdir/etc/conf.d/${_name}"
  cd "${_name}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
