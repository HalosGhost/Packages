# Maintainer: Sam Stuewe <halosghost at archlinux dot info>
# Contributor: Stefan Seering

pkgname='hashcat'
pkgver=0.47
pkgrel=1
pkgdesc='A multithreaded cross platform hash cracker.'
arch=('i686' 'x86_64')
_url='https://hashcat.net'
url="${_url}/${pkgname}/"
license=('custom:proprietary')
depends=('glibc')
install="${pkgname}.install"
source=("${_url}/files/${pkgname}-${pkgver}.7z"
        "${install}")
sha256sums=('239acb25b88d529314f2f98af0d6a66772e886c9efbb4ed2b94b7587c9a68455'
            '22d30b179f3f4e287c1e757ddf371941f1e934d0356a1a3a4fde1333dfbf7045')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  if [[ "$CARCH" == 'x86_64' ]]; then
    mv hashcat-cli64.bin hashcat
  else
    mv hashcat-cli32.bin hashcat
  fi
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
 
  rm *.{exe,bin,app}
 
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  mkdir -p "${pkgdir}/etc/${pkgname}"
  
  install -Dm755 hashcat "${pkgdir}/usr/bin/${pkgname}"

  cp -dpr --no-preserve=ownership "${srcdir}/${pkgname}-${pkgver}"/{rules,salts,tables} "${pkgdir}"/etc/"${pkgname}"/
  cp -dpr --no-preserve=ownership "${srcdir}/${pkgname}-${pkgver}"/examples "${pkgdir}"/usr/share/doc/"${pkgname}"/
}

# vim:set ts=2 sw=2 et:
