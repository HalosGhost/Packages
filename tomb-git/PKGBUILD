# Maintainer: Sam Stuewe <halosghost at archlinux dot org>
# Contributor: David Lo Re <boyska@gmail.com>
# Contributor: nignux <nignux@freaknet.org>
_name=tomb
pkgname="${_name}-git"
pkgver=416.914ac95
pkgrel=1
pkgdesc="A simple tool to manage encrypted storage"
arch=('i686' 'x86_64')
url="http://tomb.dyne.org/"
license=('GPL')
depends=('bc' 'cryptsetup' 'gnupg' 'gtk2>=2.16' 'libgcrypt'
         'libnotify' 'pinentry' 'sudo' 'zsh')
makedepends=('git')
optdepends=('steghide: steganography support' 
            'dcfldd: dd progress bar support'
            'wipe: secure delete support')
install="${_name}.install"
provides=("${_name}")
source=("${_name}::git://github.com/dyne/${_name//t/T}.git"
        "${install}"
        "_${_name}")
sha256sums=('SKIP'
            '81ed942ba0595f8d8786e4f4d2ce5f653e7d88de785485e049736986da5633a7'
            '73a220f6e25990081f227cd78af7226935ad517bfe4898f24408ee5cc7c17315')

pkgver() {
   cd "${_name}"
   echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
   cd "${_name}/extras/kdf"
   make
}

package() {
   install -Dm644 "_${_name}" "${pkgdir}/usr/share/zsh/site-functions/_${_name}"
   cd "${_name}"
   make DESTDIR="${pkgdir}" PREFIX=/usr install
   cd extras/kdf
   make DESTDIR="${pkgdir}" PREFIX=/usr install
}
